apply plugin: 'continuuity'

apply plugin: 'application'
mainClassName = 'com.continuuity.SingleNodeMain'


buildscript {
  apply from: 'continuuity.gradle'
}

/**
 * Specify all the dependencies
 */
dependencies {

    /// Internal dependencies.
    if(multiModule)
    {
        compile project(":common")
        compile project(":gateway")
        compile project(":flow")
        compile project(":data-fabric")
        compile project(":overlord")
        runtime group: 'com.continuuity', name: 'flow', classifier: 'api', version: '0.1.0-SNAPSHOT'
        runtime group: 'com.continuuity', name: 'data-fabric', classifier: 'api', version: '0.1.0-SNAPSHOT'
    }
    else
    {
        compile group: 'com.continuuity', name: 'common', version: '0.1.0-SNAPSHOT'
        compile group: 'com.continuuity', name: 'data-fabric', version: '0.1.0-SNAPSHOT'
        compile group: 'com.continuuity', name: 'flow', version: '0.1.0-SNAPSHOT'
        compile group: 'com.continuuity', name: 'gateway', version: '0.1.0-SNAPSHOT'
        compile group: 'com.continuuity', name: 'overlord', version: '0.1.0-SNAPSHOT'
    }

    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.+'
}

/**
 * Exclude any config xml from singlenode jar
 */
jar.exclude '*.xml'


task javadoc(type: Javadoc, overwrite: true) {
    group = 'Documentation' 
    description = 'Aggregates API javadocs across all projects.'
    source project.parent.subprojects.collect { project ->
        project.sourceSets.api.allJava
    }
    destinationDir = new File(buildDir, 'javadoc')
    // Might need a classpath
    classpath = files(project.parent.subprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })
}

/**
 * Configure distZip task 
 */
tasks.distZip {
    baseName = "bigFlow"
    appendix = "singlenode"
    version = ""
    classifier = ""
}

distZip.dependsOn javadoc
distZip.dependsOn ':web-cloud-app:build'

applicationDistribution.from(sourceSets.main.output.resourcesDir) {
    into "conf"
}
applicationDistribution.from("$buildDir/javadoc") {
    into "docs"
}

//web-cloud-app
applicationDistribution.from(project(':web-cloud-app').file('build/client')) {
    into "client"
}

applicationDistribution.from(project(':web-cloud-app').file('build/server')) {
    into "server"
}


/**
 * Hack the start script  - set application name, remain in $APP_HOME and add $APP_HOME/conf/ to classpath
 */

tasks.startScripts {
    applicationName = "bigFlow"
    doLast {
        def scriptFile = file "${outputDir}/${applicationName}"
        scriptFile.text = scriptFile.text.replace('cd "$SAVED"', '')
        scriptFile.text = scriptFile.text.replaceFirst('CLASSPATH=(.*)', 'CLASSPATH=$1:\\$APP_HOME/conf/')
        //for windows .bat - yes, apparently backslash must be triple-escaped
        def scriptFileWin = file "${outputDir}/${applicationName}.bat"
        scriptFileWin.text = scriptFileWin.text.replaceFirst('set CLASSPATH=(.*)', 'set CLASSPATH=$1;%APP_HOME%\\\\conf\\\\')
    }
}

/**
 * Collect bin scripts from gateway project and replace @classpath@ token
 */

import org.apache.tools.ant.filters.ReplaceTokens

applicationDistribution.from(project(':gateway').file('bin')) {
    into "bin"
    def cp = startScripts.classpath.collect { "\$APP_HOME/lib/${it.name}" }.join(':')
    cp += ":\$APP_HOME/conf/"
    filter(ReplaceTokens, tokens: [classpath: cp])
}

///**
// * Collect bin scripts from flow project and replace @classpath@ token
// */
//applicationDistribution.from(project(':flow').file('bin')) {
//    into "bin"
//    def cp = startScripts.classpath.collect { "\$APP_HOME/lib/${it.name}" }.join(':')
//    cp += ":\$APP_HOME/conf/"
//    filter(ReplaceTokens, tokens: [classpath: cp])
//}

/**
 * Collect bin scripts from data-fabric project and replace @classpath@ token
 */
applicationDistribution.from(project(':data-fabric').file('bin')) {
    into "bin"
    def cp = startScripts.classpath.collect { "\$APP_HOME/lib/${it.name}" }.join(':')
    cp += ":\$APP_HOME/conf/"
    filter(ReplaceTokens, tokens: [classpath: cp])
}


/**
 * Collect node start script from web-cloud-app project
 */
applicationDistribution.from(project(':web-cloud-app').file('bin/start_webapp.sh')) {
    into "bin"
}

