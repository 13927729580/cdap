apply plugin:'java'
/**
 * Specify all the dependencies
 */
dependencies {

    compile elastic([group: 'com.continuuity', name: 'flow', version: "${version}", changing: true], "flow")
    compile elastic([group: 'com.continuuity', name: 'flow', classifier: 'api', version: "${version}", changing: true], "flow")
    compile elastic([group: 'com.continuuity', name: 'data-fabric', version: "${version}", changing: true], "data-fabric")
    compile elastic([group: 'com.continuuity', name: 'data-fabric', classifier: 'api', version: "${version}", changing: true], "data-fabric")
    compile elastic([group: 'com.continuuity', name: 'gateway', version: "${version}", changing: true], "gateway")

    compile libraries.jetty
    compile libraries.netflix_curator_framework
    compile libraries.netflix_curator_client
    compile libraries.netflix_curator_discovery_client
    compile libraries.netflix_curator_discovery_server
    compile libraries.netflix_curator_recipes
    compile libraries.hadoop_hdfs
    compile libraries.http_client
}

// skip the main java tasks - we only build the test jar in this module
jar.onlyIf { }
sourcesJar.onlyIf { }
javadocJar.onlyIf { }

/**
 * Exclude any config xml from singlenode jar
 */
jar.exclude '*.xml'

// ideally we would use a name of "continuuity" and classifier of "test"
// unfortunately there is a known gradle bug that will prevent uploading of
// the pom if the artifact has a classifier. Tried many ways to work around
// that and finally gave up. Hence we use "continuuity-test" as the artifact
// name. The gradle bugs is discussed here with links to the Jiras:
// http://forums.gradle.org/gradle/topics/pom_doesnt_upload

task testJar(type: Jar) {
    baseName = "continuuity-test"
    from sourceSets.test.output
}

artifacts {
    archives testJar
}

// add a filter to the upload task that only passes the test jar. Otherwise
// we would be uploading test.jar, test-sources.jar, test-javadoc.jar and
// test.pom, when all we have is really the test jar

uploadArchives {
    repositories.mavenDeployer {
        addFilter('test') {artifact, file ->
            artifact.name == 'continuuity-test'
        }
    }
}
